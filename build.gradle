apply plugin: 'com.github.ben-manes.versions'
apply from: 'buildsystem/dependencies.gradle'

def loadProperties(String sourceFileName) {
    def config = new Properties()
    def propFile = file(sourceFileName)

    System.err.println("Loading property file: " + propFile.absolutePath)

    if (propFile.canRead()) {
        config.load(new FileInputStream(propFile))
        for (Map.Entry property in config) {
            println "setting " + property.key
            project.ext[property.key] = property.value;
        }
    }
}

buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
        maven { url 'https://jitpack.io' }
    }

    def gradleVersionsVersion = '0.12.0'
    def androidToolsVersion = "2.1.2"
    def aptVersion = "1.8"
    def gradleRetrolambdaVersion = "3.3.0-beta4"

    dependencies {
        classpath "com.github.ben-manes:gradle-versions-plugin:$gradleVersionsVersion"
        classpath "com.android.tools.build:gradle:$androidToolsVersion"
        classpath "com.neenbedankt.gradle.plugins:android-apt:$aptVersion"
        classpath "me.tatarka:gradle-retrolambda:$gradleRetrolambdaVersion"
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.5.2'
    }
}

allprojects {
    ext {
        loadProperties("build_number")
        BUILD_NUMBER = rootProject.ext.has("BUILD_NUMBER") ? BUILD_NUMBER as int : 1

        androidApplicationId = 'ru.kodep.sample.sampleproject'
        androidVersionCode = BUILD_NUMBER
        androidVersionName = "0.0.$BUILD_NUMBER"
        testInstrumentationRunner = "android.support.test.runner.AndroidJUnitRunner"
        testApplicationId = 'ru.entirec.kindneighbour.test'

        println "androidVersionName=$androidVersionName"
    }

    repositories {
        maven { url "https://jitpack.io" }
        maven { url "http://dl.bintray.com/populov/maven" }
        maven { url "http://dl.bintray.com/tbruyelle/tbruyelle" }
        maven { url "https://repo.commonsware.com.s3.amazonaws.com" }
        maven { url 'https://maven.fabric.io/public' }
        maven { url "repo" }
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
